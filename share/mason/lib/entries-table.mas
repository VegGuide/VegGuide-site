<& /lib/current-filters.mas, search => $search &>

% if ($show_radius_change) {
<& /lib/radius-change.mas, search => $search &>
% }
<table id="entries-table" class="standard-table <% $search->can('latitude') ? 'with-distance' : '' %>">
  <thead>
    <tr>
      <td colspan="<% $show_add_entry ? $colspan - 1 : $colspan %>">
        <% $pagination %>
      </td>
% if ($show_add_entry) {
      <td>
        <a class="content-button" href="<% region_uri( location => $search->location(), path => 'entry_form' ) %>" title="add a new entry to this region">Add Entry</a>
      </td>
% }
    </tr>

    <tr class="table-heading">
      <th class="name"><&| /lib/sort-link.mas, search => $search &>Name</&></th>
%   if ( $search->can('latitude') ) {
      <th class="distance"><&| /lib/sort-link.mas, search => $search &>Distance</&></th>
%   }
      <th class="summary">Summary</th>
      <th class="rating"><&| /lib/sort-link.mas, search => $search &>Rating</&></th>
      <th class="how-veg"><&| /lib/sort-link.mas, search => $search &>How Veg?</&></th>
     </tr>
   </thead>

% if ( $pager->total_entries() ) {
   <tfoot>
     <tr>
       <td colspan="<% $colspan %>">
         <% $pagination %>
       </td>
     </tr>
   </tfoot>
% }

%   while ( my $vendor = $vendors->next ) {
<& .one-vendor, search => $search, vendor => $vendor, count => $vendors->count() &>
%   }

</table>

<%args>
$search
$pager
$show_radius_change => 0
</%args>

<%init>
my $vendors = $search->vendors();

my $pagination =
    $m->scomp( '/lib/pagination.mas',
               pager  => $pager,
               search => $search,
               things => 'entries',
             );

my $colspan = $search->can('latitude') ? 5 : 4;

my $show_add_entry = 0;
if ( $search->can('location') )
{
    $show_add_entry = $c->vg_user()->is_admin() || $search->location()->can_have_vendors() ? 1 : 0;
}
</%init>

<%def .one-vendor>
<tbody>
  <tr class="<% $count % 2 ? 'odd' : 'even' %>">
    <td class="name">
      <& /lib/link/vendor.mas, vendor => $vendor &>
% if (@address) {
      <address>
%   for my $line (@address) {
        <% $line | h %><% $line ne $address[-1] ? '<br/>' : '' %>
%   }
      </address>
% }
% if ( $vendor->is_closed ) {
      <div class="closed">Closed as of <& /lib/format/date.mas, date => $vendor->close_date_object &></div>
% }
    </td>
% if ($distance) {
    <td class="distance">
      <% $distance %> <% PL( $search->unit(), $distance ) %>
    </td>
% }
    <td class="summary">
      <span class="summary-text"><% $vendor->short_description | h %></span>
% my $cat = join ', ', map { $_->name() } $vendor->categories();
% if ( $cat ne 'Restaurant' ) {
      <br /><span class="category"><% $cat %></span>
% }
    </td>

    <td class="rating">
% unless ( $vendor->is_organization() ) {
<& /lib/stars-for-rating.mas, rating => $weighted_rating, color => 'green', title => $rating_title &>
<br />
%   if ($user_rating) {
<strong>\
%   }
<% $rating_count %> <% PL( 'rating', $rating_count ) %>\
%   if ($user_rating) {
</strong>
%   }
% }
    </td>
    <td class="how-veg veg-level-<% $vendor->veg_level() %>">
% unless ( $vendor->is_organization() ) {
      <% $vendor->veg_description() | h %>
% }
    </td>
  </tr>
</tbody>

<%args>
$search
$vendor
$count
</%args>

<%init>
my ( $weighted_rating, $rating_count ) = $vendor->weighted_rating_and_count();

my $user_rating = $vendor->rating_from_user( $c->vg_user() );

my $rating_title;

if ($weighted_rating)
{
    $rating_title = "$weighted_rating, with $rating_count " . PL( 'rating', $rating_count );
    $rating_title .= " (you rated it a $user_rating)" if $user_rating;
}
else
{
    $rating_title = "no ratings";
}

my @address;
@address = $vendor->address_pieces()
    if $search->has_addresses();

my $distance;
if ( $search->can('latitude') )
{
    $distance =
        $vendor->distance_from
            ( latitude  => $search->latitude(),
              longitude => $search->longitude(),
              unit      => $search->unit(),
            );
}
</%init>
</%def>
